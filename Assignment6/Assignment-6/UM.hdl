CHIP UM {
    IN x[8], y[8];
    OUT out[16];
    PARTS:

    // Partial product (PP) generation
    PPgen(a[0..7] = x, a[8..15] = false, b = y[0], out = pp0);
    PPgen(a[0] = false, a[1..8] = x, a[9..15] = false, b = y[1], out = pp1);
    PPgen(a[0..1] = false, a[2..9] = x, a[10..15] = false, b = y[2], out = pp2);
    PPgen(a[0..2] = false, a[3..10] = x, a[11..15] = false, b = y[3], out = pp3);
    PPgen(a[0..3] = false, a[4..11] = x, a[12..15] = false, b = y[4], out = pp4);
    PPgen(a[0..4] = false, a[5..12] = x, a[13..15] = false, b = y[5], out = pp5);
    PPgen(a[0..5] = false, a[6..13] = x, a[14..15] = false, b = y[6], out = pp6);
    PPgen(a[0..6] = false, a[7..14] = x, a[15] = false, b = y[7], out = pp7);  

    // Summand Addition Tree

    // Level 0 to 1
    CSA3to2(a = pp0, b = pp1, c = pp2, s = s10, carry = c10);
    CSA3to2(a = pp3, b = pp4, c = pp5, s = s11, carry = c11);

    // Level 1 to 2
    CSA3to2(a = c10, b = s10, c = c11, s = s20, carry = c20);
    CSA3to2(a = s11, b = pp6, c = pp7, s = s21, carry = c21);

    // Level 2 to 3
    CSA3to2(a = c20, b = s20, c = c21, s = s30, carry = c30);

    // Level 3 to 4
    CSA3to2(a = c30, b = s30, c = s21, s = s40, carry = c40);



    // 16 - bit adder using higher order propagate and generate terms
    HigherOrderCLA(a = s40, b = c40, op = false, out = out, of = claof);
    
    
}